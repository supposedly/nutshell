@RUEL RockPaperScissors
From "RockScissorsPaperLife" by Dave Greene

@TABEL
states: 4
neighborhood: Moore
symmetries: permute

# Dead cells for State 1
deadA = {0, 2, 3}
# Dead cells for State 2
deadB = {0, 1, 3}
# Dead cells for State 3
deadC = {0, 1, 2}

# Birth in competitive circumstances
{0, 1}, 1:3, 3:3, deadC, 3
{0, 2}, 2:3, 1:3, deadA, 1
{0, 3}, 3:3, 2:3, deadB, 2

# Birth
{0, 1}, 3:3, deadC, 3
{0, 2}, 1:3, deadA, 1
{0, 3}, 2:3, deadB, 2

# the RockScissorsPaper rules
{1..3}, [0: {3, 1, 2}], any:7, 0

# Three-neighbor survival
# these can't be moved into the Birth transitions above, because
# 'the RockScissorsPaper rules' have to override them
1, 1:3, deadA, 1
2, 2:3, deadB, 2
3, 3:3, deadC, 3

# Two-neighbor survival
1, 1:2, deadA, 1
2, 2:2, deadB, 2
3, 3:2, deadC, 3

# Death
any, any, 0